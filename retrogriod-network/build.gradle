plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def githubProperties = new Properties()
def propertiesFile = rootProject.file("github.properties")
if (propertiesFile.exists() && propertiesFile.canRead()) {
    githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
}

android {
    namespace 'com.app.retrogriod.network'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.app.retrogriod.network'
                artifactId 'retrogrid'
                version '1.0.3'
            }

            debug(MavenPublication) {
                from components.debug
                groupId 'com.app.retrogriod.network'
                artifactId 'retrogrid'
                version '1.0.3'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/DeepuGeorgeJacob/retrogrid")

                credentials {
                    username = githubProperties['git.user'] ?: project.getProperty('USER_NAME')
                    password = githubProperties['git.key'] ?: project.getProperty('TOKEN')
                }
            }
        }
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.20"

}